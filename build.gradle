plugins {
    id 'idea'
    id 'java-library'
    id 'net.neoforged.moddev' version '2.0.116'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = 'Bawnorton'
                url = 'https://maven.bawnorton.com/releases/'
            }
        }
        filter {
            includeGroup 'com.github.bawnorton.mixinsquared'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'BlameJared'
                url = 'https://maven.blamejared.com/'
            }
        }
        filter {
            includeGroup 'com.almostreliable.mods'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sinytra'
                url = 'https://maven.su5ed.dev/releases/'
            }
        }
        filter {
            includeGroup 'org.sinytra'
            includeGroup 'org.sinytra.forgified-fabric-api'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sleeping Town'
                url = 'https://repo.sleeping.town/'
            }
        }
        filter {
            includeGroup 'folk.sisby'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'TerraformersMC'
                url = 'https://maven.terraformersmc.com/releases/'
            }
        }
        filter {
            includeGroup 'dev.emi'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Modrinth'
                url = 'https://api.modrinth.com/maven/'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'CurseForge'
                url = 'https://cursemaven.com/'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    mavenCentral()
}

neoForge {
    version = project.neo_version

    parchment {
        minecraftVersion = project.parchment_minecraft_version
        mappingsVersion = project.parchment_mappings_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
        "sheepish-asm" {
            sourceSet(project(":sheepish-asm").sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    compileOnly(annotationProcessor('io.github.llamalad7:mixinextras-neoforge:0.5.0'))
    implementation(jarJar('io.github.llamalad7:mixinextras-neoforge:0.5.0'))
    compileOnly(annotationProcessor('com.github.bawnorton.mixinsquared:mixinsquared-common:0.3.4'))
    implementation(jarJar('com.github.bawnorton.mixinsquared:mixinsquared-neoforge:0.3.4'))

    implementation(jarJar('folk.sisby:kaleido-config:0.3.3+1.3.2'))

    implementation files('libs/org.sinytra.connector-2.0.0-beta.10+1.21.1-mod.jar')
    implementation 'org.sinytra.forgified-fabric-api:forgified-fabric-api:0.115.6+2.1.2+1.21.1'

    // NeoForge
    compileOnly 'maven.modrinth:akashic-tome:1.21.1-1.8-29'
    compileOnly 'com.almostreliable.mods:almostunified-neoforge:1.21.1-1.3.0:api'
    compileOnly 'maven.modrinth:armor-toughness-bar,-again:9473L0CF'
    compileOnly 'maven.modrinth:biolith:byVGeYWw'
    compileOnly 'maven.modrinth:blueprint:8.0.7'
    compileOnly 'maven.modrinth:dynamicsurroundingsfabric:aFsN201L'
    compileOnly 'dev.emi:emi-neoforge:1.1.22+1.21.1'
    compileOnly 'maven.modrinth:emojiful:drIeVIkS'
    compileOnly 'maven.modrinth:extended-industrialization:1.15.21-1.21.1'
    compileOnly 'maven.modrinth:forge-config-api-port:v21.1.6-1.21.1-NeoForge'
    compileOnly 'maven.modrinth:hexerei:0.5.0.3'
    compileOnly 'maven.modrinth:immersiveengineering:12.4.2-194'
    compileOnly 'curse.maven:lovely-sparkle-pieces-1274469:6657986'
    compileOnly 'curse.maven:minestuck-224635:6421096'
    compileOnly 'maven.modrinth:mirthdew-encore:0.2.0.3.neoforge'
    compileOnly 'maven.modrinth:modern-industrialization:2.3.15'
    compileOnly 'maven.modrinth:no-mans-land:1.4.4'
    compileOnly 'maven.modrinth:owo-lib:0.12.15.5-beta.1+1.21'
    compileOnly 'maven.modrinth:patchouli:1.21.1-92-neoforge'
    compileOnly 'maven.modrinth:platform:Idu0kQLR'
    compileOnly 'maven.modrinth:rapscallions-and-rockhoppers:2.0.6+1.21.1-neoforge'
    compileOnly 'maven.modrinth:sodium:mc1.21.1-0.6.13-neoforge'

    // Fabric
    compileOnly 'folk.sisby:surveyor:1.0.0-alpha.10+1.21'
    compileOnly 'maven.modrinth:cerulean:1.1.5'
    compileOnly 'maven.modrinth:excessive-building:3.3.10'
    compileOnly 'maven.modrinth:tinkerers-smithing:2.7.1+1.21'

    // Compile-time dependencies
    compileOnly 'com.esotericsoftware.yamlbeans:yamlbeans:1.15'
    compileOnly 'com.github.luben:zstd-jni:1.5.7-3'

    jarJar project(":sheepish-asm")
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

sourceSets {
    dummy {
        compileClasspath += configurations.compileClasspath
    }
    main {
        compileClasspath += dummy.output
    }
}